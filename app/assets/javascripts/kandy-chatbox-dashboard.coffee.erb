#= require underscore-min
#= require gmaps/google

chatData = {}

window.onerror = ->
  return false

# Create audio objects to play incoming calls and outgoing calls sound
audioRingIn = $('<audio>', { loop: 'loop', id: 'ring-in' })
audioRingOut = $('<audio>', { loop: 'loop', id: 'ring-out' })

# Load audio source to DOM to indicate call events
audioSource =
  ringIn: [
    { src: 'https://kandy-portal.s3.amazonaws.com/public/sounds/ringin.mp3', type: 'audio/mp3' }
    { src: 'https://kandy-portal.s3.amazonaws.com/public/sounds/ringin.ogg', type: 'audio/ogg' }
  ]
  ringOut: [
    { src: 'https://kandy-portal.s3.amazonaws.com/public/sounds/ringout.mp3', type: 'audio/mp3' }
    { src: 'https://kandy-portal.s3.amazonaws.com/public/sounds/ringout.ogg', type: 'audio/ogg' }
  ]

audioSource.ringIn.forEach (entry) ->
  source = $('<source>').attr('src', entry.src)
  audioRingIn.append(source)

audioSource.ringOut.forEach (entry) ->
  source = $('<source>').attr('src', entry.src)
  audioRingOut.append(source)

$(window).on 'resize', ->
  for e in $('.outgoing-video')
    $(e).css('margin-top': -$(e).height())

updateVideoViews = (delay)->
  setTimeout ->
    for e in $('.outgoing-video')
      $(e).css('margin-top': -$(e).height())
  , delay

window.loadAndStartChat = (url) ->
  jQuery.ajax
    url: url
    type: 'GET'
    dataType: 'json'
    success: (res) ->
      chatData = res

      removeLoadingOverlay()

      setInterval ->
        jQuery.ajax
          url: res.shop.host + 'chat/online'
          type: 'POST'
          dataType: 'json'
      , 15 * 1000

      updateOperatorStatus(res.me.status)
      $(document).on 'click', '#user-status-btn', ->
        jQuery.ajax
          url: chatData.shop.host + 'chat/change-user-status'
          type: 'POST'
          dataType: 'json'
          success: (res) ->
            updateOperatorStatus(res.status)
            json =
              cmd: 'update_status'
              id: chatData.me.id
              status: res.status
            for o in chatData.users
              KandyAPI.messaging.sendJSON o.kandy_username, json, undefined, (msg) ->
                displayErrorMessage(msg)

      if res.users
        for u in res.users
          $('#users-online-list').append """
            <a href='#' data-operator-id="#{u.id}" class='list-group-item'>#{u.display_name} <span class="pull-right #{u.online}">â€¢</span></a>
          """

      if res.conversations
        for c, i in res.conversations
          displayMessage(i)

updateOperatorStatus = (status) ->
  $('#user-status-btn').removeClass('hide')
  if status is 'available'
    $('#user-status-btn').removeClass('btn-warning')
    $('#user-status-btn').addClass('btn-success')
    $('#user-status-btn').text('Available')
    $('#user-status-btn').attr('data-original-title', 'You are available to chat')
  else
    $('#user-status-btn').removeClass('btn-success')
    $('#user-status-btn').addClass('btn-warning')
    $('#user-status-btn').text('Unavailable')
    $('#user-status-btn').attr('data-original-title', 'You are unavailable to chat')
  $('[data-toggle="tooltip"]').tooltip()

updateOnlineStatus = (operator) ->
  $("#users-online-list a[data-operator-id=#{operator.id}] span").removeClass('true false').addClass("#{operator.status is 'available'}")

displayMessage = (idx) ->
  c = getConversationByIndex(idx)

  time = new Date(c.created_at)

  if checkOperatorJoined(chatData.me.id, c.id)
    displayConversation(idx)
  else if c.operators.length > 0
    $('#other-active-conversations-list').append """
      <a id="conversation-#{c.id}" href='#' class='list-group-item'>
        <span class='badge'>#{time.toLocaleTimeString(navigator.language, {hour: '2-digit', minute:'2-digit'})}</span>
        <h5 class='list-group-item-heading'>#{c.title}</h5>
        <div class='list-group-item-text'>#{c.location.country_name}</div>
      </a>
    """
  else
    $('#conversations-list').append """
      <a id="conversation-#{c.id}" href='#' class='list-group-item'>
        <span class='badge'>#{time.toLocaleTimeString(navigator.language, {hour: '2-digit', minute:'2-digit'})}</span>
        <h5 class='list-group-item-heading'>#{c.title}</h5>
        <div class='list-group-item-text'>#{c.location.country_name}</div>
      </a>
    """

  $(document).on 'click', '#conversation-' + c.id, ->
    notifyOperatorJoinedChat(idx)
    displayConversation(idx)

getConversationByIndex = (idx) ->
  chatData.conversations[idx]

displayConversation = (idx) ->
  c = getConversationByIndex(idx)

  $('#conversation-' + c.id).hide()
  $('#conversations-dashboard').show()
  $('#welcome-dashboard').hide()

  unless $('#conversation-tabs-' + c.id).length
    $('#conversation-tabs').append """
      <li class="dropdown dropdown-li" id="conversation-tabs-#{c.id}" role='presentation'>
          <a class="dropdown-link disabled" href="#conversation-tabs-content-#{c.id}"
          aria-controls="conversation-tabs-content-#{c.id}" role="tab" data-toggle="tab" data-idx="#{idx}">
            #{c.title}
          </a>
          <a class="dropdown-caret dropdown-toggle" data-toggle="dropdown"><b class="caret"></b></a>
          <ul class="dropdown-menu">
              <li><a id="conversation-tabs-#{c.id}-end-btn" >End chat</a></li>
              <li><a id="conversation-tabs-#{c.id}-leave-btn">Leave chat</a></li>
              <li class="hide"><a id="conversation-tabs-#{c.id}-invite-btn">Invite operator</a></li>
          </ul>
      </li>
    """
    $('#conversation-tabs-content').append """
      <div class="tab-pane" id="conversation-tabs-content-#{c.id}" role="tabpanel">
        <div class="panel">
          <div class="panel-body">
            <div class="row in-call-box" id="in-call-box-#{c.id}" style="display:none">
              <div class="row" id="video-call-views-#{c.id}">
                <div class='video incoming-video'>
                  <video id="incoming-video-#{c.id}" autoplay width="100%" height="100%"></video>
                </div>
                <div class='video outgoing-video'>
                  <video id="outgoing-video-#{c.id}" autoplay width="100%" height="100%"></video>
                </div>
              </div>
              <div class="row pull-right">
                <button class="btn btn-xs btn-danger" id="end-btn-#{c.id}">End</button>
                <button class="btn btn-xs btn-warning" id="hold-btn-#{c.id}">Hold</button>
              </div>
            </div>
            <div class="row messages-display-container">
              <div class="list-group">
              </div>
            </div>
            <div class="row send-form-container">
              <form>
                <div class="form-group">
                  <div class="col-md-10 col-sm-9">
                    <textarea class="form-control" rows="3" placeholder="Type and press [enter]"></textarea>
                  </div>
                  <div class="col-md-2 col-sm-3 col-xs-12">
                    <div class="pad-top hidden-lg hidden-md pull-right">
                      <button class="btn btn-sm btn-success send-btn-#{c.id}">
                        <i class="fa fa-send"></i>
                      </button>
                      <button class="btn btn-sm btn-primary call-btn-#{c.id}">
                        <i class="fa fa-video-camera"></i>
                      </button>
                    </div>
                    <div class="hidden-xs hidden-sm">
                      <button class="btn btn-sm btn-success send-btn-#{c.id}">
                        <i class="fa fa-send"></i>
                      </button>
                      <div class="pad-top">
                        <button type="button" class="btn btn-sm btn-primary" id="call-popover-btn-#{c.id}" data-toggle="popover">
                          <i class="fa fa-video-camera"></i>
                        </button>
                      </div>
                    </div>
                  </div>
                </div>
              </form>
            </div>
          </div>
        </div>
      </div>
    """

    if kandyVideoCallEnabled
      $("#call-popover-btn-#{c.id}").popover
        html: true
        placement: 'bottom'
        container: 'body'
        content: ->
          return """
          <div class="form-inline" style="min-width: 156px">
            <div class="checkbox">
              <label>
                <input type="checkbox" id="start-with-video-checkbox-#{c.id}"> Video enabled
              </label>
            </div>
            <button class="btn btn-sm btn-primary" id="call-btn-#{c.id}">Call</button>
          </div>
          """
      $(document).on 'click', "#call-btn-#{c.id}", (e) ->
        e.preventDefault()

        startWithVideo = $("#start-with-video-checkbox-#{c.id}").prop('checked')
        $("#video-call-views-#{c.id}").hide() unless startWithVideo

        KandyAPI.call.makeCall(c.full_user_id, startWithVideo)
        $("#call-popover-btn-#{c.id}").popover('hide')
    else
      $(document).on 'click', "#call-popover-btn-#{c.id}", (e) ->
        e.preventDefault()
        KandyAPI.call.makeCall(c.full_user_id, false)

    $(document).on 'click', "#conversation-tabs-#{c.id}-end-btn", ->
      notifyOperatorCloseChat(idx)
    $(document).on 'click', "#conversation-tabs-#{c.id}-leave-btn", ->
      notifyOperatorLeftChat(idx)
    $(document).on 'click', "#conversation-tabs-#{c.id}-invite-btn", ->
      inviteNewOperator(idx)

    displayHistoryMessages(idx)

    displayInfoVisitor(idx)
    $(document).on 'click', "#conversation-tabs-#{c.id}", ->
      if $("#conversation-tabs-#{c.id}").html()
        displayInfoVisitor(idx)
        updateVideoViews(0)
        elems = "#conversation-tabs-content-#{c.id} .messages-display-container"
        $(elems).scrollTop($(elems)[0].scrollHeight)

    $(document).on 'keypress', "#conversation-tabs-content-#{c.id} form textarea", (e) ->
      typingChecking()
      if e.keyCode is 13
        msg = $.trim($("#conversation-tabs-content-#{c.id} form textarea").val())
        if msg isnt ''
          sendNewMessageToVisitor(idx, msg)
        $("#conversation-tabs-content-#{c.id} form textarea").val('')
        removeTyping()
        return false

    $(document).on 'click', ".send-btn-#{c.id}", (e) ->
      e.preventDefault()
      msg = $.trim($("#conversation-tabs-content-#{c.id} form textarea").val())
      if msg isnt ''
        sendNewMessageToVisitor(idx, msg)
      $("#conversation-tabs-content-#{c.id} form textarea").val('')

    $(document).on 'click', "#end-btn-#{c.id}", ->
      KandyAPI.call.endCall(c.callId)

    $(document).on 'click', "#hold-btn-#{c.id}", ->
      if $(this).text() == 'Hold'
        KandyAPI.call.holdCall(c.callId)
        $(this).text('Resume')
      else
        KandyAPI.call.unHoldCall(c.callId)
        $(this).text('Hold')

    $('.dropdown-toggle').dropdown()

  $("#conversation-tabs-#{c.id} > a:first-child").tab('show')
  elems = "#conversation-tabs-content-#{c.id} .messages-display-container"
  $(elems).scrollTop($(elems)[0].scrollHeight)

displayHistoryMessages = (idx) ->
  c = getConversationByIndex(idx)

  for m in c.messages
    if m.is_joined || m.is_left || m.is_closed
      appendStatusChange(m.id, m.text)
    else if m.type == 'o'
      appendOperatorMessage(m.id, m.display_name, m.avatar, m.text, m.timestamp)
    else if m.type == 'v'
      appendVisitorMessage(m.id, m.display_name, m.text, m.timestamp)

sendNewMessageToVisitor = (idx, text) ->
  c = getConversationByIndex(idx)
  id = c.id

  message =
    id: id
    type: 'o'
    text: text
    cmd: 'new_message'
    avatar: chatData.me.avatar
    display_name: chatData.me.display_name
    user_id: chatData.me.id
    timestamp: new Date().getTime()

  #appendOperatorMessage(id, chatData.me.display_name, chatData.me.avatar, text, message.timestamp)
  sendMessageToGuestAndOperators(idx, message)
  saveMessage(id, message)

appendOperatorMessage = (id, name, avatar, msg, time) ->
  DateTime = new Date(Number(time))
  Time = DateTime.toLocaleString()

  elems = "#conversation-tabs-content-#{id} .messages-display-container"
  if $(elems).length
    if !avatar || avatar == ''
      avatar = '<%= asset_url('avatars/avatar-operator.png') %>'
    $("#{elems}  .list-group").append """
      <div class="list-group-item agent-message">
        <div class="media">
          <div class="media-left">
            <img class="media-object" src="#{avatar}">
          </div>
          <div class="media-body">
            <h4 class="media-heading">#{name} <small>- #{Time}</small></h4>
              #{msg}
          </div>
        </div>
      </div>
    """
    $(elems).scrollTop($(elems)[0].scrollHeight)

appendVisitorMessage = (id, name, msg, time) ->
  DateTime = new Date(Number(time))
  Time = DateTime.toLocaleString()

  elems = "#conversation-tabs-content-#{id} .messages-display-container"
  if $(elems).length
    $("#{elems}  .list-group").append """
      <div class="list-group-item visitor-message">
        <div class="media">
          <div class="media-left">
            <img class="media-object" src="<%= asset_url('avatars/visitor-avatar-thick.png') %>">
          </div>
          <div class="media-body">
            <h4 class="media-heading">#{name} <small>- #{Time}</small></h4>
            #{msg}
          </div>
        </div>
      </div>
    """
    $(elems).scrollTop($(elems)[0].scrollHeight)

appendStatusChange = (id, msg) ->
  elems = "#conversation-tabs-content-#{id} .messages-display-container"
  $("#{elems}  .list-group").append """
    <div class="pad-top pad-bottom status-message">#{msg}</div>
  """
  $(elems).scrollTop($(elems)[0].scrollHeight)

typingChecking = ->

removeTyping = ->

sendMessageToGuestAndOperators = (idx, message) ->
  c = getConversationByIndex(idx)

  KandyAPI.messaging.sendJSON c.full_user_id, message, undefined, (msg) ->
    console.log(message)
    displayErrorMessage(msg)

  for o in chatData.users
    KandyAPI.messaging.sendJSON o.kandy_username, message, undefined, (msg) ->
      console.log(message)
      displayErrorMessage(msg)

saveMessage = (id, msg) ->
  jQuery.ajax
    url: chatData.shop.host + 'chat/save'
    type: 'post'
    dataType: 'json'
    data:
      id: id
      message: msg
    success: ->

displayInfoVisitor = (idx) ->
  conversation = getConversationByIndex(idx)

  $('#conversation-detail').html """
    <div class="row">
      <b>Name: </b> #{conversation.name}
    </div>
    <div class="row">
      <b>Email: </b> #{conversation.email}
    </div>
    <div class="row">
      <b>IP: </b> #{conversation.location.ip}
    </div>
  """

  $('#conversation-history').html ->
    html = "<ul>"
    for h in conversation.histories
      if h.created_at isnt conversation.created_at
        html += "<li><a href='#{chatData.shop.host + '/preferences/transcripts?id=' + h.id}' target='_blank'>#{new Date(h.created_at).toLocaleString()}</a></li>"
    html += "</ul>"

  $('#conversation-location').html('')

  shopGmap 'conversation-location',
      latitude: conversation.location.latitude
      longitude: conversation.location.longitude

checkOperatorJoined = (userId, chatId) ->
  for c in chatData.conversations
    if c.id == chatId
      for o in c.operators
        if o.id == userId
          return true
  return false


notifyOperatorJoinedChat = (idx) ->
  c = getConversationByIndex(idx)
  if c.status is 'close'
    return
  id = c.id

  unless checkOperatorJoined(chatData.me.id, c.id)
    jQuery.ajax
      url: chatData.shop.host + 'chat/join'
      type: 'POST'
      dataType: 'json'
      data:
        id: id
      success: (res) ->
        message =
          id: id
          cmd: 'new_message'
          type: 'o'
          username: chatData.me.kandy_username
          display_name: chatData.me.display_name
          avatar: chatData.me.avatar
          role: chatData.me.role
          is_joined: true
          user_id: chatData.me.id
          text: "#{chatData.me.display_name} #{chatData.shop.widget.joined_chat}"
          timestamp: new Date().getTime()

        sendMessageToGuestAndOperators(idx, message)
        saveMessage(id, message)
      error: (e) ->
        displayErrorMessage(e)

notifyOperatorLeftChat = (idx) ->
  c = getConversationByIndex(idx)
  if c.status is 'close'
    return
  id = c.id

  $("#conversation-tabs-#{id}").remove()
  $("#conversation-tabs-content-#{id}").remove()

  if $('#conversation-tabs').html()
    $('#conversation-tabs > li:last-child > a:first-child').tab('show')
  else
    $('#conversation-detail').html('')
    $('#conversation-location').html('')

    if !$('#conversation-tabs').html() || $('#conversation-tabs').html() is ''
      $('#conversations-dashboard').hide()
      $('#welcome-dashboard').show()

  if checkOperatorJoined(chatData.me.id, id)
    jQuery.ajax
      url: chatData.shop.host + 'chat/leave'
      type: 'POST'
      dataType: 'json'
      data:
        id: id
      success: (res) ->
        message =
          id: id
          cmd: 'new_message'
          type: 'o'
          display_name: chatData.me.display_name
          is_left: true
          user_id: chatData.me.id
          text: "#{chatData.me.display_name} #{chatData.shop.widget.left_chat}"
          timestamp: new Date().getTime()

        sendMessageToGuestAndOperators(idx, message)
        saveMessage(id, message)
      error: (e) ->
        console.log(e)

notifyOperatorCloseChat = (idx) ->
  c = getConversationByIndex(idx)
  if c.status is 'close'
    return
  id = c.id
  jQuery.ajax
    url: chatData.shop.host + 'chat/close'
    type: 'POST'
    dataType: 'json'
    data:
      id: id
    success: () ->
      message =
        cmd: 'notify_close_chat'
        id: id
        type: 'o'
        is_closed: true
        display_name: chatData.me.display_name
        text: "#{chatData.me.display_name} #{chatData.shop.widget.closed_chat}"
        timestamp: new Date().getTime()

      sendMessageToGuestAndOperators(idx, message)
      saveMessage(id, message)

closeChat = (id, msg) ->
  for c in chatData.conversations
    if c.id is id
      c.status = 'close'
      break

  if $("#conversation-tabs-#{id}").html()
    $("#conversation-tabs-#{id} .dropdown-menu").hide()
    appendStatusChange(id, msg)
    $("#conversation-tabs-content-#{id} .send-form-container").html """
      <center>
        <a href="#{chatData.shop.host + '/chat/download?id=' + id}" class="btn" alt="Download" data-toggle="tooltip" data-placement="bottom" title="Download">
          <i class="fa fa-cloud-download fa-2x"></i>
        </a>
        <a class="btn" id="conversation-tabs-#{id}-email-btn" alt="Email"  data-toggle="tooltip" data-placement="bottom" title="Email">
          <i class="fa fa-envelope-o fa-2x"></i>
        </a>
        <a href="#" class="btn hide" id="conversation-tabs-#{id}-ban-btn" alt="Ban IP"  data-toggle="tooltip" data-placement="bottom" title="Ban IP">
          <i class="fa fa-ban fa-2x"></i>
        </a>
        <a href="#" class="btn" id="conversation-tabs-#{id}-close-btn" alt="Close"  data-toggle="tooltip" data-placement="bottom" title="Close">
          <i class="fa fa-times fa-2x"></i>
        </a>
      </center>
    """
    $('[data-toggle="tooltip"]').tooltip()

    $(document).on 'click', "#conversation-tabs-#{id}-email-btn", ->
      sendMailConversation(id)
    $(document).on 'click', "#conversation-tabs-#{id}-ban-btn", ->
      banVisitorIP(id)
    $(document).on 'click', "#conversation-tabs-#{id}-close-btn", ->
      closeConversationTab(id)
  else
    closeConversationTab(id)

sendMailConversation = (id) ->
  swal
    title: "An input!"
    text: "Enter your email:"
    type: "input"
    showCancelButton: true
    animation: "slide-from-top"
    inputPlaceholder: "Your email"
    showLoaderOnConfirm: true
    closeOnConfirm: false
  , (email) ->
      if email is false
        return false
      if email is "" || !(/^([a-zA-Z0-9_\.\-])+\@.(([a-zA-Z0-9\-])+\.)+([a-zA-Z0-9]{2,5})+$/).test(email)
        swal.showInputError("You need to enter a valid email!")
        return false
      $.ajax
        url: "#{chatData.shop.host}/chat/send-mail-transcript"
        type: 'POST'
        dataType: 'json'
        data:
          id: id
          email: email
        success: (res) ->
          swal('Done!', 'A email has been sent to you.', 'success')


banVisitorIP = (id) ->

inviteNewOperator = (id) ->

closeConversationTab = (id) ->
  $("#conversation-#{id}").remove()
  $("#conversation-tabs-#{id}").remove()
  $("#conversation-tabs-content-#{id}").remove()

  if $('#conversation-tabs').html()
    $('#conversation-tabs > li:last-child > a:first-child').tab('show')
  else
    $('#conversation-detail').html('')
    $('#conversation-location').html('')
    if !$('#conversation-tabs').html() || $('#conversation-tabs').html() is ''
      $('#conversations-dashboard').hide()
      $('#welcome-dashboard').show()

displayErrorMessage = (msg) ->
  console.log(msg)

window.handleEventListeners = (data) ->
  json = JSON.parse(data.message.json)
  if isValidChat(json)
    switch json.cmd
      when 'new_chat'
        if json.domain_id && json.domain_id isnt '' && Number(json.domain_id) == chatData.shop.id
          addNewChat(json)
      when 'new_message'
        addNewMessage(json)
      when 'notify_close_chat'
        closeChat(json.id, json.text)
      when 'update_status'
        updateOnlineStatus(json)

addNewChat = (conversation) ->
  chatData.conversations.push(conversation)
  idx = chatData.conversations.length - 1

  time = new Date(conversation.created_at)
  $('#conversations-list').append """
    <a id="conversation-#{conversation.id}" href='#' class='list-group-item'>
      <span class='badge'>#{time.toLocaleTimeString()}</span>
      <h5 class='list-group-item-heading'>#{conversation.title}</h5>
      <div class='list-group-item-text'>#{conversation.location.country_name}</div>
    </a>
  """

  $(document).on 'click', '#conversation-' + conversation.id, ->
    notifyOperatorJoinedChat(idx)
    displayConversation(idx)

addNewMessage = (message) ->
  for c in chatData.conversations
    if Number(c.id) is Number(message.id)
      c.messages.push(message)
      if message.is_joined
        c.operators.push
          id: message.user_id
          kandy_username: message.username
        if checkOperatorJoined(chatData.me.id, c.id)
          appendStatusChange(message.id, message.text)
        else
          $('#other-active-conversations-list').prepend($('#conversation-' + c.id))
      else if message.is_left
        for o, i in c.operators
          if o.id == message.user_id
            c.operators.splice(i, 1)
            break
        if checkOperatorJoined(chatData.me.id, c.id)
          appendStatusChange(message.id, message.text)
        else
          $('#conversation-' + c.id).show()
          if c.operators.length > 0
            $('#other-active-conversations-list').prepend($('#conversation-' + c.id))
          else
            $('#conversations-list').prepend($('#conversation-' + c.id))
      break

  if !message.is_joined && !message.is_left
    if message.type == 'o'
      appendOperatorMessage(message.id, message.display_name, message.avatar,message.text, message.timestamp)
    else if message.type == 'v'
      appendVisitorMessage(message.id, message.display_name, message.text, message.timestamp)

isValidChat = (json) ->
  return json

window.onCallInitiate = (call) ->
  audioRingIn[0].pause()
  audioRingOut[0].play()

  console.log call
  console.log call.getRemoteVideoState()

  idx = $("#conversation-tabs li[class*='active'] a[data-toggle='tab']").data('idx')
  c = getConversationByIndex(idx)
  c.callId = call.getId()
  $(".call-btn-#{c.id}").addClass('disabled')
  $("#in-call-box-#{c.id}").show()
  unless kandyVideoCallEnabled
    $("#video-call-views-#{c.id}").hide()

  $("#outgoing-video-#{c.id}").attr('src', null)
  $("#incoming-video-#{c.id}").attr('src', call.localStreamURL)
  call.onStreamAdded = (url) ->
    $("#incoming-video-#{c.id}").attr('src', url)
    $("#outgoing-video-#{c.id}").attr('src', call.localStreamURL)
    updateVideoViews(1500)
  updateVideoViews()
  #appendStatusChange(c.id, "Calling #{c.title}...")
  swal
    title: "Calling #{c.title}..."
    confirmButtonColor: "#DD6B55"
    confirmButtonText: "End"
    , ->
      KandyAPI.call.endCall(call.getId())

window.onCallInitiateFail = ->
  audioRingOut[0].pause();
  idx = $("#conversation-tabs li[class*='active'] a[data-toggle='tab']").data('idx')
  c = getConversationByIndex(idx)
  c.callId = undefined
  $(".call-btn-#{c.id}").removeClass('disabled')
  $("#in-call-box-#{c.id}").hide()
  swal('Oops...', 'call initiate failed!', 'error')
  #appendStatusChange(c.id, "Error: call initiate failed.")

getVisitorFromCall = (call) ->
  callee = call.callerNumber
  for c in chatData.conversations
    if c.full_user_id is callee
      return c
  return undefined

window.onCallIncoming = (call) ->
  #console.log call.canReceiveVideo()
  c = getVisitorFromCall(call)
  if c.callId
    KandyAPI.call.reject(call.getId())
  else
    audioRingIn[0].play();
    c.callId = call.getId()
    call.onStreamAdded = (url) ->
      $("#incoming-video-#{c.id}").attr('src', url)
      updateVideoViews(1500)
    call.onLocalStreamAdded = (url) ->
      $("#outgoing-video-#{c.id}").attr('src', call.localStreamURL)
      updateVideoViews(1500)

    swal
      title: "#{c.title} is calling..."
      showCancelButton: true
      cancelButtonColor: "#DD6B55"
      confirmButtonColor: "#449d44"
      confirmButtonText: "Accept"
      cancelButtonText: "Reject"
      closeOnConfirm: false
    , (isConfirm)->
        if isConfirm
          if kandyVideoCallEnabled
            swal
              title: "Video Call"
              text: "Accept this call with video sharing enabled?"
              showCancelButton: true
              cancelButtonColor: "#DD6B55"
              confirmButtonColor: "#449d44"
              confirmButtonText: "Yes"
              cancelButtonText: "No"
            , (isConfirm2)->
              $("#video-call-views-#{c.id}").hide() unless isConfirm2
              KandyAPI.call.answerCall(c.callId, isConfirm2)
          else
            KandyAPI.call.answerCall(c.callId, false)
          $('#conversation-tabs-10').tab('show')
        else
          KandyAPI.call.rejectCall(c.callId)

window.onCallAnswer = (call) ->
  c = getVisitorFromCall(call)
  audioRingIn[0].pause();
  $("#in-call-box-#{c.id}").show()
  unless kandyVideoCallEnabled
    $("#video-call-views-#{c.id}").hide()

window.onCallRejected = (call) ->
  c = getVisitorFromCall(call)
  c.callId = undefined
  audioRingIn[0].pause();
  swal.close()

window.onCall = (call) ->
  console.log call
  console.log call.canReceiveVideo()

  audioRingOut[0].pause();
  swal.close()

window.onCallTerminate = (call)->
  audioRingOut[0].pause();
  audioRingIn[0].pause();
  swal.close()
  for c in chatData.conversations
    if c.callId == call.getId()
      c.callId = undefined
      $("#video-call-views-#{c.id}").show() if kandyVideoCallEnabled
      $(".call-btn-#{c.id}").removeClass('disabled')
      $("#in-call-box-#{c.id}").hide()
      #appendStatusChange(c.id, "Call terminated.")
      break